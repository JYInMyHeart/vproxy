plugins {
    id 'java'
}

group 'vproxy'
version loadVersion()

sourceCompatibility = 11.0

repositories {
    mavenCentral()
}

dependencies {
    // make sure no `compile` dependencies appear here

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'io.vertx', name: 'vertx-redis-client', version: '3.7.0'
    testCompile group: 'io.vertx', name: 'vertx-web-client', version: '3.7.0'
    testCompile group: 'io.grpc', name: 'grpc-all', version: '1.20.0'
    testCompile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    testCompile group: 'org.apache.thrift', name: 'libthrift', version: '0.11.0'
    testCompile group: 'com.alibaba', name: 'dubbo', version: '2.6.6'
}

jar {
    archiveName = "vproxy.jar"
    manifest {
        attributes 'Main-Class': 'vproxy.app.Main'
    }
}

def loadVersion() {
    def PREFIX = "this.version = \""
    def SUFFIX = "\"; // _THE_VERSION_"
    def ver = file(sourceSets.main.java.srcDirs[0].path + "/vproxy/app/Application.java")
    def lines = ver.getText().split("\n")
    for (def line : lines) {
        line = line.trim()
        if (line.startsWith(PREFIX) && line.endsWith(SUFFIX)) {
            return line.substring(PREFIX.length(), line.length() - SUFFIX.length())
        }
    }
    return "unknown"
}

task stage(dependsOn: ['clean', 'jar'])
jar.mustRunAfter clean
